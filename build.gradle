plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'net.ltgt.apt' version '0.6'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id "com.github.sherter.google-java-format" version "0.6"
}


version = file('src/main/resources/VERSION').text.trim()

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

sourceSets {
    integTest
}

configurations {
    bundle
    compile.extendsFrom bundle
    integTestCompile.extendsFrom testCompile
}

dependencies {
    compile gradleApi()
    bundle(project(':format')) {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    bundle      'com.google.guava:guava:19.0'
    bundle      'com.google.dagger:dagger:2.5'
    apt         'com.google.dagger:dagger-compiler:2.5'
    apt         'com.google.auto.value:auto-value:1.2'
    compileOnly 'com.google.auto.value:auto-value:1.2'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'com.google.jimfs:jimfs:1.1'

    integTestCompile project(':test')
    integTestCompile gradleTestKit()
    integTestCompile sourceSets.main.output
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors()
}

if (System.env.TRAVIS == 'true') {
  allprojects {
    tasks.withType(GroovyCompile) {
      groovyOptions.fork = false
    }
    tasks.withType(Test) {
      // containers (currently) have 2 dedicated cores and 4GB of memory
      maxParallelForks = 2
      minHeapSize = '128m'
      testLogging {
        events 'passed', 'skipped', 'failed'
        showStackTraces true
        exceptionFormat 'full'
      }
    }
  }
}

def integTestGradleVersion = System.getenv('GRADLE_VERSION') ?: '2.0'

task integrationTest(type: Test, dependsOn: publishToMavenLocal) {
    group 'Verification'
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    systemProperty 'GRADLE_VERSION', integTestGradleVersion
    reports.html.destination = new File(reportsDir, 'integrationTest' + File.separator + integTestGradleVersion)
}
check.dependsOn integrationTest
integrationTest.mustRunAfter test


tasks.shadowJar {
  classifier = null // necessary for com.gradle.plugin-publish to pick this artifact up
  configurations = [project.configurations.bundle]
  relocate('dagger', 'com.github.sherter.googlejavaformat.dagger')
  relocate('com.google.common', 'com.github.sherter.googlejavaformatgradleplugin.com.google.common')
  exclude('META-INF/maven/**/*')
}
// removes the dependsOn relation between "publishPlugins" and "jar" and forces
// the correct (the shadowed) artifact to be uploaded to the plugin portal
configurations.archives.artifacts.clear()
artifacts {
  archives tasks.shadowJar
}

pluginBundle {
    website = 'https://github.com/sherter/google-java-format-gradle-plugin'
    vcsUrl = 'https://github.com/sherter/google-java-format-gradle-plugin.git'
    description = 'Format your Java source files with google-java-format'
    tags = ['java', 'format', 'style']

    plugins {
        googleJavaFormatGradlePlugin {
            id = 'com.github.sherter.google-java-format'
            displayName = 'google-java-format gradle plugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.sherter.googlejavaformatgradleplugin'
            artifactId 'google-java-format-gradle-plugin'

            from components.shadow
        }
    }
    if (version.endsWith('-SNAPSHOT')) {
        repositories {
            maven {
                name = 'SonatypeSnapshot'
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username System.env.SONATYPE_SNAPSHOTS_USERNAME
                    password System.env.SONATYPE_SNAPSHOTS_PASSWORD
                }
            }
        }
    }
}
